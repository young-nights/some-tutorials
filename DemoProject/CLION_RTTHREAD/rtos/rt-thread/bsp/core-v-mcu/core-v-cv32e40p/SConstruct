import os
import sys
import rtconfig
from SCons.Script import *

if os.getenv('RTT_ROOT'):
    RTT_ROOT = os.getenv('RTT_ROOT')
else:
    RTT_ROOT = os.path.normpath(os.getcwd() + '/../../..')
print(RTT_ROOT)
sys.path = sys.path + [os.path.join(RTT_ROOT, 'tools')]# 模块搜索路径
try:
    from building import *
except:
    print('Cannot found RT-Thread root directory, please check RTT_ROOT')
    print(RTT_ROOT)
    exit(-1)

TARGET = 'rtthread.' + rtconfig.TARGET_EXT

DefaultEnvironment(tools=[])
env = Environment(tools = ['mingw'],
    AS = rtconfig.AS, ASFLAGS = rtconfig.AFLAGS,
    CC = rtconfig.CC, CFLAGS = rtconfig.CFLAGS,
    AR = rtconfig.AR, ARFLAGS = '-rc',
    CXX = rtconfig.CXX, CXXFLAGS = rtconfig.CXXFLAGS,
    LINK = rtconfig.LINK, LINKFLAGS = rtconfig.LFLAGS)
env.PrependENVPath('PATH', rtconfig.EXEC_PATH)

if rtconfig.PLATFORM in ['iccarm']:
    env.Replace(CCCOM = ['$CC $CFLAGS $CPPFLAGS $_CPPDEFFLAGS $_CPPINCFLAGS -o $TARGET $SOURCES'])
    env.Replace(ARFLAGS = [''])
    env.Replace(LINKCOM = env["LINKCOM"] + ' --map project.map')

Export('RTT_ROOT')
Export('rtconfig')

SDK_ROOT = os.path.abspath('./') #当前脚本所在路径的绝对地址
#SDK_ROOT = E:\rt-thread\bsp\core-v-mcu\core-v-cv32e40p

if os.path.exists(SDK_ROOT + '/Libraries'):
    libraries_path_prefix = SDK_ROOT + '/Libraries'
else:
    libraries_path_prefix = os.path.dirname(SDK_ROOT) + '/Libraries'
#libraries_path_prefix = E:\rt-thread\bsp\core-v-mcu\Libraries
SDK_LIB = libraries_path_prefix
Export('SDK_LIB')
#SDK_LIB = E:\rt-thread\bsp\core-v-mcu\Libraries

# prepare building environment
objs = PrepareBuilding(env, RTT_ROOT, has_libcpu=False)

core_v_library = 'core_v_hal_libraries'
rtconfig.BSP_LIBRARY_TYPE = core_v_library

bsp_vdir = 'build'
library_vdir = 'build/libraries'

#libraries_path_prefix = E:\rt-thread\bsp\core-v-mcu\Libraries
#objs.extend 将扩展列表的元素都加到 objs列表中
# include libraries
objs.extend(SConscript(os.path.join(libraries_path_prefix, core_v_library, 'SConscript'), variant_dir=library_vdir + '/core_v_hal_libraries', duplicate=0))

# common include drivers
objs.extend(SConscript(os.path.join(libraries_path_prefix, 'core_v_drivers', 'SConscript'), variant_dir=library_vdir + '/core_v_drivers', duplicate=0))

# make a building
DoBuilding(TARGET, objs)
